import com.fasterxml.jackson.databind.ObjectMapper
import java.nio.file.Paths

buildscript {
    repositories {
        mavenCentral()
        maven {
            url = uri("https://packages.confluent.io/maven/")
        }
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
        maven {
            url = uri("https://jitpack.io")
        }
    }
}


plugins {
    id 'idea'
    id 'eclipse'
    id "com.google.protobuf" version "0.8.17"
    id "com.github.imflog.kafka-schema-registry-gradle-plugin" version "1.1.1"
    id "com.github.davidmc24.gradle.plugin.avro" version "1.0.0"
}

group 'io.confluent.developer'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven {
        url = uri("https://packages.confluent.io/maven/")
    }

    maven {
        url = uri("https://jitpack.io")
    }
}

dependencies {
    implementation platform('software.amazon.awssdk:bom:2.10.73')
    implementation 'software.amazon.awssdk:lambda:2.17.19'
    implementation 'org.apache.kafka:kafka-clients:2.8.0'
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.9.0'
    implementation 'software.amazon.awssdk:secretsmanager:2.17.19'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'

    implementation 'com.google.protobuf:protobuf-java:3.17.3'
    implementation 'org.apache.avro:avro:1.10.2'
    implementation('org.apache.kafka:kafka-clients:2.8.0!!')
    implementation('io.confluent:kafka-streams-avro-serde:6.1.1') {
        exclude group: 'org.apache.kafka', module: 'kafka-clients'
        exclude group: 'org.apache.kafka', module: 'kafka-streams'
    }
    implementation('io.confluent:kafka-streams-protobuf-serde:6.1.1') {
        exclude group: 'org.apache.kafka', module: 'kafka-clients'
        exclude group: 'org.apache.kafka', module: 'kafka-streams'
    }
    implementation "io.confluent:kafka-avro-serializer:6.1.1"
    implementation "io.confluent:kafka-protobuf-serializer:6.1.1"
    implementation "io.confluent:kafka-protobuf-provider:6.1.1"


    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j18-impl:2.14.1'
    runtimeOnly 'com.amazonaws:aws-lambda-java-log4j2:1.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

protobuf {
    generatedFilesBaseDir = "${project.buildDir}/generated-main-proto-java"

    protoc {
        artifact = 'com.google.protobuf:protoc:3.15.3'
    }
}

/*
  This task looks for a file named 'confluent.properties' in the
  src/main/resources directory and creates a JSON file 'aws-cli/aws-ccloud-creds.json'
  and is used by both the create-secret and update-secret scripts for safely
  storing your CCloud connection credentials
 */

class PropertiesToJsonTask extends DefaultTask {
    @TaskAction
    def propsToJson() {
        ObjectMapper objectMapper = new ObjectMapper();
        Properties properties = new Properties();
        try (FileInputStream fis = new FileInputStream("src/main/resources/confluent.properties")) {
            properties.load(fis);
        }
        // Taking these out because they aren't sensitive values plus we want the same entries between different deployments
        ["security.protocol", "sasl.mechanism", "client.dns.lookup",
         "acks", "basic.auth.credentials.source"].forEach(k -> properties.remove(k))
        String saslConfig = properties.get("sasl.jaas.config")
        // We're doing this because the AWS security manager wants a separate username and password entry for configuring the event source
        def matcher = saslConfig =~ /.*username='([^']*)'.*password='([^']*)'.*/
        if (matcher.find()) {
            properties.put("username", matcher.group(1))
            properties.put("password", matcher.group(2))
        } else {
            throw new RuntimeException("AWS security manager requires 'username' and 'password' entries ")
        }
        File destinationJsonFile = new File("aws-cli/aws-ccloud-creds.json")
        objectMapper.writerWithDefaultPrettyPrinter().writeValue(destinationJsonFile, properties);
    }
}

tasks.register("propsToJson", PropertiesToJsonTask)



task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}
